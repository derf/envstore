#!/usr/bin/env perl
## envstore - save and load environment variables
## Copyright Â© 2009 by Daniel Friesel <derf@derf.homelinux.org>
## License: WTFPL <http://sam.zoy.org/wtfpl>
use strict;
use warnings;
use Simplestore;

my $store_file = "/tmp/.envstore-$>";
my %store;
my $action = shift;
my $arg = shift;
my $arg2 = shift;
my ($key, $value);

sub usage {
	print STDERR "Invalid invocation - see perldoc -F $0\n";
	exit(1);
}

sub check_store {
	my ($mode, $uid);
	unless (-e $store_file) {
		return(0);
	}
	($mode, $uid) = (stat($store_file))[2,4];
	$mode &= 0x00077;
	if ($uid != $<) {
		print STDERR "envstore: store file not owned by us\n";
		exit(1);
	}
	if ($mode > 0) {
		print STDERR "envstore: store file writable by group/others\n";
		exit(1);
	}
	return(1);
}

sub load_store {
	return unless check_store;
	%store = %{load($store_file) || {}};
}

sub save_store {
	umask(0077);
	save($store_file, \%store);
}

sub get_keyvalue {
	my ($key, $value) = @_;
	unless (defined($value)) {
		if (exists($ENV{$key})) {
			$value = $ENV{$key};
		} else {
			print STDERR "No such parameter: $key\n";
			exit(1);
		}
	}
	return($key, $value);
}

usage unless defined($action);
load_store;
if ($action eq 'save') {
	usage unless defined($arg);
	($key, $value) = get_keyvalue($arg, $arg2);
	$store{$key} = $value;
	save_store;
} elsif ($action eq 'eval') {
	while (($key, $value) = each(%store)) {
		$value =~ s/'/'"'"'/g;
		print "export $key='$value'\n";
	}
} elsif ($action eq 'show' or $action eq 'list') {
	while (($key, $value) = each(%store)) {
		printf("%-15s = %s\n", $key, $value);
	}
} elsif ($action eq 'rm') {
	usage unless defined($arg);
	delete($store{$arg});
	save_store;
} elsif ($action eq 'clear') {
	unlink($store_file);
} else {
	usage;
}
__END__

=head1 NAME

envstore - save and restore environment variables

=head1 SYNOPSIS

B<envstore> I<command> [ I<arguments> ]

=head1 DESCRIPTION

envstore can safe and restore environment variables, thus transferring them
between different shells.

I<command> must be one of

=over

=item B<clear>

Forget all stored variables

=item B<eval>

Produce shell code for evaluation, restoring all saved variables

=item B<save> I<variable> [I<value>]

Save I<variable> either with it's current shell value or with I<value>

=item B<show>

List saved variables in better readable format

=item B<rm> I<variable>

Remove I<variable> from store

=back
